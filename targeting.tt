#nop Targeting aliases and events

#class targeting kill;
#class targeting open;

#read targetlist.tt;

registerEvent RoomInfo targeting.onRoomInfo;
#alias {targeting.onRoomInfo}
{
   #local roomInfo $gmcp[room][info];

   #nop UNhighlight mobs from a different area.;
   #if {{$areaName} != {$roomInfo[area]}} {
      #local areaList $targets[$areaName];
      #foreach *areaList[] tar {
         #local tar *areaList[+tar]; 
         #unhighlight $tar; 
      };
   };

   #var areaName $roomInfo[area];

   #nop highlight new area's targets;
   #local areaList $targets[$areaName];
   #foreach *areaList[] tar {
      #highlight { $tar } {blink reverse};
   };
}

registerEvent CharItemsAdd targeting.onCharItemsAdd;
#alias {targeting.onCharItemsAdd}
{
   #local loc $gmcp[char][items][add][location];

   #if {"$loc" === "inv"} {#return};

   #local item $gmcp[char][items][add][item];
   #local icon $item[icon];
   #if {"$icon" == "deadbody"} { #return; };

   #local attri $item[attrib];

   #if {"$attri" == "%*t%*"} { 
      #local TakableItem $item[name]; #nop will be used in the event getItems;
      raiseEvent getItems;
   };

   #if {"$attri" == "" || "$attri" != "%*m%*" }
   { 
      #local name $item[name];
      #local id $item[id];
      #local item <279>$name<028>$id<099>;

      room_window_show items $item;
   };

   #if {"$attri" == "%*m%*" }
   { 
      #local name $item[name];
      #local id $item[id];
      #local mob <279>$name<028>$id<099>;
      #var roomMobs[$id] {$name};
      room_window_show mobs $mob;
   };
}

registerEvent CharItemsList targeting.onCharItemsList;
#alias {targeting.onCharItemsList}
{
   #local loc $gmcp[char][items][list][location];
   #if {"$loc" == "inv"} {#return};

   #list room_window[tab][mobs] clear;
   #list room_window[tab][items] clear;
   #unvar roomMobs;
   room_window_draw_data;

   #local items $gmcp[char][items][list][items];
   #foreach $items[%*] item {

      #local icon $item[icon];
      #if {"$icon" == "deadbody"} { #continue; };

      #local attri $item[attrib];
      #nop ADD items here;
      #if {"$attri" == "" || "$attri" != "%*m%*" }
      { 
         #local name $item[name];
         #local id $item[id];
         #local item <279>$name<028>$id<099>;

         room_window_show items $item;
         #if {"$attri" == "%*t%*"} { 
            #local TakableItem $name; #nop will be used in the event getItems;
            raiseEvent getItems;
         };
         #continue; 
      };

      #if {"$attri" == "%*d%*" }{ #continue; };

      #local name $item[name];
      #local id $item[id];
      #local mob <279>$name<028>$id<099>;
      #var roomMobs[$id] {$name};

      room_window_show mobs $mob;
   }
}

registerEvent CharItemsRemove targeting.onCharItemsRemove;
#alias {targeting.onCharItemsRemove}
{
   #local loc $gmcp[char][items][remove][location];
   #if {"$loc" == "inv"} {#return};

   #local item $gmcp[char][items][remove][item];
   #local id $item[id];

   room_window_remove $id;
   #unvar roomMobs[$id];
}

#alias {targets}
{
   #if {"%1" === ""}
   {
      #showme {<149>Targets for <819>[<839>$areaName<819>]<099>};
      #foreach {*targets[$areaName][%*]} tar {
         echo   $tar;
      };
   };
   #elseif {&targets[$areaName][%0]}
   {
      #unhighlight {%0};
      #unvar targets[$areaName][%0];
      info Target '%0' removed from '$areaName'.;
      #class targetlist write targetlist.tt;
   };
   #else
   {
      #highlight { %0 } {blink reverse};
      #var targets[$areaName][%0] {};
      info Target '%0' added to '$areaName'.;
      #class targetlist write targetlist.tt;
   };
}

#alias {atk}
{
   #nop If there is already a target, focus that one down.;
   #if {&{atkTarget} != 0} {
      #foreach *roomMobs[] denizenid {
         #if {$denizenid == $atkTarget} {
            attackCmd $atkTarget;
            #return;
         };
      };
   };

   #unvar atkTarget;

   #if {"%1" === ""}
   {
      #if {&{roomMobs} == 0} {#return};
      #if {&{roomMobs[]} == 0} {#return};

      #foreach *roomMobs[] denizenid {
         #foreach *targets[$areaName][] tar {

            #if {"$roomMobs[$denizenid]" == "%*$tar%*"} {
               dbg Setting target to: $denizenid, checked against $tar;
               #var atkTarget $denizenid;
               #send {settarget $denizenid};
               attackCmd $denizenid;

               #local name $roomMobs[$denizenid];
               #local mob <419>$name<079>$denizenid<099>;

               room_window_highlight mobs $denizenid $mob;
               #return;
            };
         };
      };
   };
   #else
   {
      #var atkTarget %1;
      attackCmd %1;
   };
}

#class targeting close

#alias {whois} { 
   #format inquery %n %1;
   #var inquery $chardb[$inquery];

   echo Class: $inquery[class];
   echo Level: $inquery[lvl];
   echo PvP: $inquery[pvp];
   echo City: $inquery[color]$inquery[city]<099>;
}

