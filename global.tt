#nop

#nop #config verbose on;

#event {RECEIVED ERROR}
{
   #echo <118>%h { TOKENIZER };
   #info tokenizer -1;
   #echo <118>%h
}

#script {configdir} {echo $HOME/.config/tintin}
#read {$configdir[1]/guisettings.tt}

#var seenPlys {};

#read movecommands.tt;
#read eventsSystem.tt;
#read connect.tt;
#read gui.tt;
#read targeting.tt

#nop Convert ANSI output to HTML
#nop install 'aha'
#nop  cat test.txt | aha --black > test.htm

#nop to see ANSI in NVIM, 
#nop  use nvim -c ":term cat $1", or ansivim in .profile.


#config {command color} {\e[4;33m}; #nop underline yellow;
#config {verbatim}      {on};
#config {REPEAT_ENTER}  {ON};    #nop repeat last command.

#var getgold 1;
#alias {ggold} {
   #if {$getgold == 1 || "%1" == "0"} {
      #var getgold 0;
      deregisterEvent getItems getGold;
      info Stopped auto get gold;
   };
   #else {$getgold == 0 || "%1" == "1"} {
      #var getgold 1;
      registerEvent getItems getGold;
      info Auto getting gold;
   };
}

#var addfree {queue add freestanding};
#var insfree {queue insert freestanding};

#alias {title %1 %2} {
}

#alias {banner} {
}

#alias {print} {
   #line ignore #showme  <169>--------------------------------------------------------------------------------;
   #line ignore #showme  <079>             [ <139>%0 <079>]<099>;
   #line ignore #showme  <169>--------------------------------------------------------------------------------;
}

#alias {err} {
   #line ignore #showme  {<431>ERROR: <408>%0<099>};
}

#alias {info} {
   #line ignore #showme  {<438>%0<099>};
}

#alias {dbg} {
   #showme  {<405>DBG: <208>%0<099>};
}

#alias {pvpInfo} {
}

#alias {journal} {
   #nop #if {{%0} != {}} { #log off; #config {log} {txt}; #break; };

   #config log raw;
   #log append journal.txt;

   #show {};
   #show {};
   print {Begin Entry};
}

#alias {fight!} {
   #nop #if {{%0} != {}} { #log off; #config {log} {txt}; #break; };

   #nop #config log html;
   #log append combat.log;

   #show {};
   #show {};
   print {New Fight, use log off to stop it.};
}

#highlight {^You have slain%*.$} {black b magenta};
#action {^You have slain%*.$} {
   #unvar atkTarget;
};

#highlight {gold{|en} sovereigns} {black b yellow};

#nop Use Diagnose  for losing eq;
#nop use Directory for losing balance;

#highlight {Your fellow citizen, %w, has just escaped imprisonment in the foul pygmy dungeon. Welcome him to %w!} {black b cyan};

#alias {allowPrism} {
   #class prism clear;
   print Use 'blockPrism' to put the defence back up.;
}

#alias {blockPrism} {
   #class prism load;
}

#class prism kill;
#class prism open;
#action {^A beam of prismatic light suddenly shoots into the room.$} {
   #highlight {%*} { black b red };
   #send {*gmcp[room][info][exits][+1]};
};
#class prism close;
#class prism save;

#highlight {%1 suddenly appears, having travelled down the beam of prismatic light.} { black b red };

#var clearCharDb 0;

#alias {cw} { 
   #var clearCharDb 1;
   #send {cw};
}

#alias {hw} { 
   #var clearCharDb 1;
   #send {hw};
}

#alias {qw} { 
   #var clearCharDb 1;
   #send {qw};
}

#alias {ih} { 
   #var clearCharDb 1;
   #send {info here};
};

#read itemslist.tt;
#read events.tt;
#read autobashing.tt;

#var bftarget {};
#alias {bclear} {#var bftarget {}};
#alias {bfind} {
   #if {"%0" === "$bftarget"} {
      #math findidx {$findidx - 1};
      info Find index = $findidx;
      #buffer find $findidx %i%0;
      info finding next.;
   };
   #else {
      #var findidx -1;
      #var bftarget %0;
      #buffer find $findidx %i%0;
      info finding first, %0;
   }
}

#var useRebounding 0;
#alias {rebound} {
   #if {$useRebounding === 0} {
      #var useRebounding 1;
      #send {curing priority defense rebounding 10};
   };
   #else {
      #var useRebounding 0;
      #send {curing priority defense rebounding reset};
   }
}

/*
   List of things this could be
   reckless
   slickness
*/
#var checkReckless 0;
#var poisonCounter 0;
#action {You are confused as to the effects of the venom} {
   #sub {%0} {<119>!!!! <117>%0<119> !!!};

   #math poisonCounter {$poisonCounter + 1};
   #if {$poisonCounter > 2} {
      #send {$insfree 3 diag};
      #var poisonCounter 0;
   };

   #var checkReckless 1;
   raiseEvent ConfusedVenom;
}

#action {Your starburst tattoo flares as the world is momentarily tinted red.} {
   #show <118>--------------------------------------------------;
   #line oneshot #sub {%*} {<103>                    STARBURST USED                    };
   #show <118>--------------------------------------------------;
};

#action {^A %w tattoo fades from view and disappears} {
   print Lost %1 tattoo!;
}

/*
must pass in a number
*/
#function {pvpTarget} {
   #local count &pvpTargets[];
   #nop check if value in bounds;
   #if {%1 < 0 && %1 > $count} {#return ""};
   #if {"%1" == ""} {#return ""};

   info Target is: <118>$pvpTargets[%1][name]<988>;
   #return $pvpTargets[%1][name];
}

#high {A nearly invisible magical shield forms around%*$} { underscore <FFF0000>}

#alias {ghelp} {
  print Global helper commands;
  info * atk;
  info   - single attack a denzien in the room that is on the targets list;
  info * aatk;
  info   - auto attack denziens in the room;
  info * satk;
  info   - stop auto attacking;
  info * targets;
  info   - lists, adds/removes targets to attack;
  info * mapping; 
  info   - prints commands related to QoL for rooms/maps;
  info * occult; 
  info   - prints commands for occultists;
}


