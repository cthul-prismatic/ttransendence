#nop Functionaility for autonomous bashing;

#var ReverseDir {
   {n} {s}
   {e} {w}
   {s} {n}
   {w} {e}

   {ne} {sw}
   {nw} {se}
   {se} {nw}
   {sw} {ne}

   {up} {down}
   {down} {up}

   {in} {out}
   {out} {in}
}

#var RecordedPath { };
#var ReturnPath { };

/** 
   Raised after successfully moving to a new location
   Teleporting clears the stack
*/
deregisterEvent recordPath;
registerEvent recordPath;
#alias {recordPath} {
   #nop AttemptedDir comes from MoveCommands.tt and mapping.tt;
   #list RecordedPath ins +1 $AttemptedDir;
   #nop dbg Recorded: $RecordedPath, came in $AttemptedDir;
};

/**
   Clear the stack from teleporting
*/
deregisterEvent clearPath;
registerEvent clearPath;
#alias {clearPath} {
   #list RecordedPath clear;
};


/** Options are
   Idle     - Not activily doing anything
   Hunt     - Normal hunting mode
   Retreat  - Leave room to recover
   Heal     - Stay put until healed, used once a room is cleared from mobs and retreating
   Escape   - Hermit out to safety
   Home     - Finished bashing
   Travel   - Move to new location
*/
#var BashStrats {
   {Idle} {}
   {Hunt} {}
   {Retreat} {}
   {Heal} {}
   {Escape} {}
   {Home} {}
   {Travel} {}
};

#var AutoBashStrat {
   autobash.hunt;
}

#alias autobash.hunt {
   atk;

   #nop If health is too low, get out of there.;
   #local hp @getHpPercent{};
   #if {$hp < 0.1} {

      deregisterEvent explore $AutoBashStrat;
      #var AutoBashStrat {autobash.switchToretreat};
      registerEvent explore $AutoBashStrat;
      $AutoBashStrat;
   };

   #if {&{atkTarget} == 0 } {
      #if {$hp > 95} {
         registerEvent finishAutoHarvest continueFromHarvest;
         autoharvest;
      };
      #else {
         autobash.switchToheal;
      };
   };
}

#alias autobash.switchToheal {
   print Switching to Heal;

   deregisterEvent explore $AutoBashStrat;
   #var AutoBashStrat {autobash.heal};
   registerEvent heal $AutoBashStrat;
   $AutoBashStrat;
}

#alias autobash.heal {
   info Checking Heatlh...;

   #nop CURING SIPHEALTH;
   #local hp @getHpPercent{};
   #if {$hp > 0.95} {
      info Health is in a good state, move on.;
      deregisterEvent heal;

      #var AutoBashStrat {autobash.hunt};
      registerEvent explore $AutoBashStrat;
      $AutoBashStrat;
   };

   #else {
      print Healing...;
   }
}

#alias autobash.switchToretreat {
   print Switching to Retreat;
   deregisterEvent explore $AutoBashStrat;
   #var AutoBashStrat {autobash.retreat};
   registerEvent explore $AutoBashStrat;
   registerEvent heal $AutoBashStrat;

   $AutoBashStrat;
}

#alias autobash.retreat {
   #if {&roomMobs[] > 0} {
      #nop Back out to previous room;
      #nop In lower level places, this is enough;
      #nop But in higher level areas the mobs will follow and we need to run more.;
      #local next $RecordedPath[+1];
      #list RecordedPath delete +1;
      #list ReturnPath ins $next;

      #local dir ReverseDir[$next];
      move $dir; 
   }
}

#function {getHpPercent} {
   #var hp $gmcp[char][vitals][hp];
   #math hp {$hp * 1.0};
   #var maxhp $gmcp[char][vitals][maxhp];
   #math maxhp {$maxhp * 1.0};
   #math hpPercent {$hp/$maxhp};
   #return $hpPercent;
}

#action {You may drink another health or mana elixir.} {
   raiseEvent heal;
}


